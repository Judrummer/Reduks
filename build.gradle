// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlinVersion = '1.0.3'
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url 'http://oss.sonatype.org/content/repositories/snapshots'
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath 'com.android.tools.build:gradle:2.1.2'
        classpath "com.netflix.nebula:gradle-extra-configurations-plugin:2.2.+"
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'

    }
}
allprojects {

    configurations {
        javadocDeps
    }
    //COMMON BUILD TOOL AND SDK VERSION FOR ALL MODULES
    ext {
        VERSION_NAME='1.5.2-SNAPSHOT' //GLOBAL VERSION FOR REDUKS libs
        POM_NAME='Reduks: Reduxjs for Kotlin+Android'
        GROUP= 'com.github.beyondeye.reduks'
        sourceCompatibility_ = 1.6 //I want this library to be linkable from android projects
        targetCompatibility_ = 1.6 //I want this library to be linkable from android projects
        junit_version = '4.12'
        kovenantVersion = '3.2.2'
        rxjavaVersion = '1.1.6'
        rxandroidVersion = '1.2.1'
        supportVersion = '24.0.0' //android support lib
        compileSdkVersion_ = 23
        buildToolsVersion_ = "23.0.3" //String
        minSdkVersion_ = 15 //Integer
        targetSdkVersion_ = 23 //Integer
        extraConfVersion = '2.2.+'
        firebaseVersion='9.0.2'
        gson_version='2.7'
    }
}

subprojects {
    repositories {
        mavenCentral()
//        jcenter()
//        maven {
//            url 'http://oss.sonatype.org/content/repositories/snapshots'
//        }
//        maven {
//            url 'https://oss.sonatype.org/content/groups/staging'
//        }
//        maven {
//            url "http://repository.jetbrains.com/all"
//        }
    }
    apply plugin: 'com.android.library'
    apply plugin: 'kotlin-android'
    apply plugin: 'com.github.dcendents.android-maven'
    group = 'com.github.beyondeye' //.reduks is appended automatically by jitpack

    android {
        compileSdkVersion compileSdkVersion_
        buildToolsVersion buildToolsVersion_

        defaultConfig {
            minSdkVersion minSdkVersion_
            targetSdkVersion targetSdkVersion_
            versionCode = 1
            versionName VERSION_NAME
        }

        compileOptions {
            sourceCompatibility sourceCompatibility_
            targetCompatibility targetCompatibility_
        }
        sourceSets {
            main.java.srcDirs += 'src/main/kotlin'
            test.java.srcDirs += 'src/test/kotlin'
        }
    }
    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

        testCompile "junit:junit:${junit_version}"
        testCompile 'org.assertj:assertj-core:1.7.1'
        testCompile 'nl.jqno.equalsverifier:equalsverifier:2.0.2'
    }
    //Test Logging
    tasks.withType(Test) {
        testLogging {
            events "started", "passed", "skipped", "failed"
        }
    }
}


task clean(type: Delete) {
    delete rootProject.buildDir
}

//task for updating gradle
task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
}


